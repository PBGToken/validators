module SuccessFeeModule

struct SuccessFeeStep {
    sigma: Real
    c:     Real
}

func apply_internal(alpha: Real, sigma: Real, c: Real, next: []SuccessFeeStep) -> Real {
    if (alpha < sigma) {
        0.0
    } else if (next.is_empty()) {
        (alpha - sigma)*c
    } else {
        SuccessFeeStep{sigma_next, c_next} = next.head
        
        (Real::min(alpha, sigma_next) - sigma)*c 
        + apply_internal(alpha, sigma_next, c_next, next.tail)
    }
}

func is_valid_internal(sigma: Real, c: Real, next: []SuccessFeeStep) -> Bool {
    if (sigma < 1.0) {
        false
    } else if (sigma > 10.0) { // sigma_max of 10 is a reasonable upper limit
        false
    } else if (c < 0.0) {
        false
    } else if (c > 1.0) {
        false
    } else if (next.is_empty()) {
        true
    } else {
        SuccessFeeStep{sigma_next, c_next} = next.head

        if (sigma_next <= sigma) {
            false
        } else {
            is_valid_internal(sigma_next, c_next, next.tail)
        }
    }
}

func calc_alpha(start_price: Ratio, end_price: Ratio) -> Real {
    (end_price/start_price).to_real()
}

struct SuccessFee {
    c0:    Real
    steps: []SuccessFeeStep

    const MAX_SUCCESS_FEE_STEPS: Int = 10

    func apply(self, alpha: Real) -> Real {
        apply_internal(alpha, 1.0, self.c0, self.steps)
    }   

    func is_valid(self) -> Bool {
        self.steps.length <= SuccessFee::MAX_SUCCESS_FEE_STEPS
        && is_valid_internal(1.0, self.c0, self.steps)
    }

    func calc_provisional_fee(self, n_burn: Int, alpha: Real, n_voucher_tokens: Int, delta_vouchers: Int) -> Int {
        (delta_vouchers + ((n_burn - n_voucher_tokens)*self.apply(alpha)/alpha).floor()).bound_min(0)
    }
}