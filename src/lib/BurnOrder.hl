module BurnOrderModule

import { AssetPtr } from AssetPtrModule
import { sum_lovelace } from PortfolioModule
import { current_script, get_current_input, tx } from ScriptContext
import Tokens

enum BurnOrderReturnValue {
    L {
        lovelace: Int
    }
    V {
        value: Value
    }
}

struct BurnOrder {
    return_address:   Address
    return_datum:     Data
    min_return_value: BurnOrderReturnValue
    max_price_age:    Duration

    func find_return(self) -> TxOutput {
        // TODO: take invariants out of loop for performance
        tx.outputs.find((output: TxOutput) -> {
            output.address == self.return_address
            && output.datum.switch{ // using output.datum.inline fails if an unrelated output is sent to same address without an inline datum, so we must use a switch to be safe
                Inline{d} => d == self.return_datum,
                else => false
            }
        })
    }

    func diff(self) -> Value {
        input = current_script.switch{
            burn_order_validator => get_current_input(),
            else => error("unexpected current script (BurnOrder.diff() can only be called within burn_order_validator)")
        };
            
        return = self.find_return();

        input.value - return.value
    }

    func value(self) -> Value {
        d = self.diff();

        // prevent token dust attack with vouchers (other token dust attack prevention is in self.returned_enough())
        assert(d.to_map().all((mph: MintingPolicyHash, tokens: Map[ByteArray]Int) -> {
            if (mph == Tokens::policy) {
                tokens.length <= 2
            } else {
                true
            }
        }), "can only return 1 voucher and dvp tokens when fulfilling a burn order");

        (-1) * d.delete_policy(Tokens::policy)
    }

    func value_lovelace(self, ptrs: []AssetPtr) -> Int {
        sum_lovelace(
            self.value(), 
            tx.inputs, 
            ptrs, 
            self.price_expiry()
        )
    }

    func burned_tokens(self) -> Int {
        self.diff().get(Tokens::dvp_token)
    }

    func price_expiry(self) -> Time {
        tx.time_range.end - self.max_price_age
    }

    func returned_enough(self, ptrs: []AssetPtr) -> Bool {
        self.min_return_value.switch{
            L{lovelace} => {
                self.value_lovelace(ptrs) >= lovelace
            },
            V{requested_value} => {
                actual_value = self.value();

                // prevent token dust attack with other tokens by assuring that the returned value is either ADA, or exists in the requested value
                //  we sadly can't do this while checking that sufficient value is being returned (for which need to iterate over the requested_value instead of over the actual_value)
                actual_value >= requested_value
                && actual_value.to_map().all((actual_mph: MintingPolicyHash, actual_tokens: Map[ByteArray]Int) -> {
                    if (actual_mph == MintingPolicyHash::new(#)) {
                        // can always return ADA, even if it wasn't requested
                        true
                    } else {
                        requested_tokens = requested_value.to_map().get(actual_mph);

                        actual_tokens.all_keys((actual_token_name: ByteArray) -> {
                            requested_tokens.any_key((requested_token_name: ByteArray) -> {
                                requested_token_name == actual_token_name
                            })
                        })
                    }
                })
            }
        }
    }
}