module common_PriceFeed

import { Price } from "./Price.hl"

const MAX_PRICE_FEED_AGE: Duration = Duration::MINUTE*5

struct PriceFeed {
    asset: AssetClass
    price: Price

    func verify(self, tx: Tx) -> Bool {
        tx.time_range.start - self.price.timestamp < MAX_PRICE_FEED_AGE
    }

    func get_assetclass(self) -> AssetClass {
        self.asset
    }

    func is_for_assetclass(self, ac: AssetClass) -> Bool {
        self.asset == ac
    }

    func get_price(self) -> Price {
        self.price
    }

    func find_price(tx: Tx, price_feeds: []PriceFeed, asset_class: AssetClass) -> Price {
        price_feed: PriceFeed = price_feeds.find((price_feed: PriceFeed) -> {
            price_feed.is_for_assetclass(asset_class)
        });
    
        assert(price_feed.verify(tx), "price feed too old");

        price_feed.get_price()
    }
}
